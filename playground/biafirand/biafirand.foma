clear stack
read lexc biafirand.lexc
define Lexicon;



define V [a|e|i|u|o|î|û];
define C [b|c|d|e|f|g|h|j|k|l|m|n|p|q|r|s|t|v|w|x|y|z];
define CT1 [t];
define CT2 [n|x];

define Symbol [
	"^[" 		| 
 	"^]" 		| 
 	"^VPBI^"	| 
 	"^VPDI^"	| 
 	"^TFTR^"	| 
 	"^TPST^"	| 
 	"^CAND^"	| 
 	"^CENE^"	| 
 	"^IENE^"	| 
 	"^IE^"		| 
 	"^FTRE^"	| 
 	"^PSNTDI^"	| 
 	"^PSNTDIPOST^"	| 
 	"^WIBA^"	| 
 	"^WIBE^"	| 
 	"^INF1^"	| 
 	"^INF2^"	| 
 	"^PB^" 		| 
 	"^PAST^" 	| 
	"'"		|! prefix boundary 
 	^HXX^ 	
 ] ;



!proverbs
define Proverbs [ 
	{girê} 
	| {hewil} 
	| {nas} 
	| {pêk} 
];

! bi and di are prefixes of  imperative&aspect and aspect, respectively. These are not included in the list below, because their places change according to syntactic rules. The prefixes below are always prefixed in the same slot in the template morphology of Kurdish.


define VerbPrefixes [
		[ {der} | {ber} | {da} | {pê} | {lê} | {jê}  | {hil} | {ser} | {na} | {ne} | {ve} | {vê} | {ra} | {çê} | {rû} | {tê} ] 

	| Proverbs

];



define VStemRoot [
	 !0 : 0
	{anî}: {anî}
];

define VStemRootNoAnd [
	 0 : 0
	 !{xw}  : {xw} 
];

define VStem [
	VStemRoot
	| VStemRootNoAnd
];

define VerbPrefixBI [
	   [{bi}"^VPBI^" ]  
];

define VerbPrefixDI [
	   [{di}"^VPDI^" ] 
];

define VerbPrefixPSNTDI [
	   [{di}"^PSNTDI^" ] 
];

define VerbSuffixCAND [
	   ["^CAND^"{and} ] | ["^CAND^"{end} ]
];

define VerbSuffixCENE [
	   ["^CENE^"{ênê} ] 
];

define VerbSuffixIENE [
	   ["^IENE^"{êne} ] 
];

define VerbSuffixFTRE [
	    ["^FTRE^"{ê} ]   
];

define VerbSuffixIE [
	   ["^IE^"{e} ] 
];

define VerbSuffixWIBA [
	   ["^WIBA^"{iba} ] 
];

define VerbSuffixWIBE [
	   ["^WIBE^"{ibe} ] 
];

define VerbSuffixINF1 [
	   ["^INF1^"{in} ] 
];

define VerbSuffixINF2 [
	   ["^INF2^"{în} ] 
];




define VerbLexicon [
	   [ [VStemRoot "+Past" ]:[ VStemRoot] ]
	 | [ [VStemRoot "+PresentAspect" "+Past" ]:[ VerbPrefixDI VStemRoot] ]
	 | [ [VStemRootNoAnd "+Causative" "+Past" ]:[ VStemRootNoAnd VerbSuffixCAND] ]
	 | [ [VStemRootNoAnd "+Causative" "+PresentAspect" "+Past" ]:[ VerbPrefixDI VStemRootNoAnd VerbSuffixCAND] ]
	 | [ [VStemRootNoAnd "+Causative" "+Future" ]:[ VerbPrefixBI VStemRootNoAnd VerbSuffixCENE] ]
	 | [ [VStemRootNoAnd "+Causative" "+Imperative" ]:[ VerbPrefixBI VStemRootNoAnd VerbSuffixIENE] ]
	 | [ [VStemRoot  "+Imperative" ]:[ VerbPrefixBI VStemRoot VerbSuffixIE] ]
	 | [ [VStemRootNoAnd "+Causative" "+Present" ]:[ VerbPrefixDI  VStemRootNoAnd VerbSuffixCENE] ]
	 | [ [VStemRoot "+Past" "+Wish" ]:[ (VerbPrefixBI) VStemRoot VerbSuffixWIBA] ]
	 | [ [VStemRootNoAnd "+Causative" "+Past" "+Wish" ]:[ (VerbPrefixBI) VStemRootNoAnd VerbSuffixCAND VerbSuffixWIBA] ]

	 | [ [VStemRoot  "+Future" ]:[ VerbPrefixBI VStemRoot VerbSuffixFTRE] ]
	 | [ [VStemRoot "+Present" "+Wish" ]:[ (VerbPrefixDI) VStemRoot VerbSuffixWIBA] ]
	 | [ [VStemRoot  "+Present"  ]:[ VerbPrefixPSNTDI VStemRoot "^PSNTDIPOST^" ] ]
	 !| [ [VStemRootNoAnd "+Causative" "+PresentAspect" "+Past"  "+Wish" ]:[ (VerbPrefixDI) VStemRootNoAnd VerbSuffixCAND VerbSuffixWIBA] ]
	 | [ [VStemRootNoAnd "+Causative" "+Present" "+Wish" ]:[ (VerbPrefixDI) VStemRootNoAnd VerbSuffixCAND VerbSuffixWIBE] ]
	 | [ [VStemRoot  "+Present" "+Wish" ]:[ (VerbPrefixDI) VStemRoot  VerbSuffixWIBE] ]

	 | [ [VStemRootNoAnd "+INF"  ]:[  VStemRootNoAnd VerbSuffixCAND VerbSuffixINF1] ]

	 | [ [VStemRoot "+INF"  ]:[  VStemRoot [ VerbSuffixINF1 | VerbSuffixINF2] ] ]
];

define InfinitiveVowelRules [
	  [ î -> i || CT1  "^INF2^" _ n  ]	
	.o. [ i -> î || CT2  "^INF1^" _ n  ]	
	.o. [ i -> î || CT2  "^INF2^" _ n  ]	
];



define DeleteVowelsForInfinitive [
       [ î -> 0 || î["^INF1^"|"^INF2^"] _ n]
   .o. [ î -> 0 || i["^INF1^"|"^INF2^"] _ n]
   .o. [ i -> 0 || i["^INF1^"|"^INF2^"] _ n]
   .o. [ i -> 0 || î["^INF1^"|"^INF2^"] _ n]
   .o. [ e -> 0 || _ ["^INF1^"|"^INF2^"] [i|î] n]
];

define DeleteDoubleIForWish [
       [ i -> 0 || [î|i] "^WIBA^" _ b a ]
       .o. [ i -> 0 || [î|i] "^WIBE^" _ b e ]
];

define DeleteIBeforeFutureE [
       [ î -> 0 || _ "^FTRE^" ê ]
];

define DeleteIBeforeIE [
       [ î -> 0 || _ "^IE^" e ]
];

define DeleteVowelWIBA [
       [ i -> 0 || [C & ~y]  [a|e] ["^WIBA^"|"^WIBE^"] _ b [a|e] ]
       .o. [ e -> 0 || y _ ["^WIBE^"|"^WIBA^"] i b [e|a] ]
];

define DltVowelsBfrEFTREIE [
       [ a -> 0 || _ ["^FTRE^" | "^IE^"] [ê|e] ]
   .o. [ e -> 0 || _ ["^FTRE^" | "^IE^"] [ê|e] ]
];

define DltUBfrEFTREIE [
       [ û -> 0 || _ "^FTRE^" ê ]
       .o. [ û -> 0 || _ "^IE^" e ]
       .o. [ i -> 0 || û "^WIBA^" _ b a ]
       .o. [ i -> 0 || û "^WIBE^" _ b e ]
       .o. [ {bû} -> 0 || d i "^VPDI^" _ "^WIBA^"  b a ]
       .o. [ {bû} -> 0 || d i "^VPDI^" _ "^WIBE^"  b e ]
];

define DeletePrefixBIE [
       [ i -> 0 ||  b _ "^VPBI^" "^CENE^" ê ]
       .o. [ i -> 0 ||  b _ "^VPBI^" "^CENE^" ê ]
       .o. [ i -> 0 ||  b _ "^VPBI^" "^IENE^" ê ]
];

define DltIBfrEPSNT [
       [ î -> e ||  _ "^PSNTDIPOST^"   ]
       .o. [ û -> e ||  _ "^PSNTDIPOST^"   ]
];

define PhoneticDeletions [
	DltIBfrEPSNT .o. DeleteVowelsForInfinitive .o. DeleteDoubleIForWish .o. DeleteIBeforeFutureE .o. DeletePrefixBIE .o. DeleteIBeforeIE .o. DeleteVowelWIBA .o. DltVowelsBfrEFTREIE .o. DltUBfrEFTREIE  
];


define VerbAndFutureRule [ 
		[  [.a n d.] -> e n e ||  "^VPBI^" VStemRootNoAnd "^CAND^"  _ .#. ]
	 ];

define IrrPSNTVerbANI [
	 [  {di} -> {tê} || _ "^PSNTDI^" a n î   ] 
 	.o. [  {anî} -> {nê} ||  t ê "^PSNTDI^" _   ]
];

define IrrPSNTVerbAVET [
	 [  {di} -> {t} || _ "^PSNTDI^" a v ê t   ] 
 	.o. [  {t} -> {je} ||  t  "^PSNTDI^" a v ê _   ]
];

define IrrPSNTVerbAXAFT [
	 [  {aft} -> {ive} ||  "^PSNTDI^" a x _   ] 
];
define IrrPSNTVerbAXI [
	   {î} -> {îne} ||  "^PSNTDI^" a x _    
];

define IrrPSNTVerbDA [
	   [ {a} -> {e} ||  "^PSNTDI^" d _ .#. ]
];

define IrrPSNTVerbKET [
	   {t} -> {ve} ||  "^PSNTDI^" k e _ 
];

define IrrPSNTVerbDAGIRT [
	    [ {dagirt} -> {digire} ||  "^PSNTDI^" _ "^PSNTDIPOST^" ]
	   | [ {dagirt} -> {gire} ||  "^PSNTDI^" _ "^PSNTDIPOST^" ]
];

define IrregularVerbPSNT [
	[  {ajot} -> {ajo} ||   "^PSNTDI^" _    ]
	.o. [  a -> e  ||  _ "^PSNTDIPOST^"     ]
	.o. IrrPSNTVerbANI
	.o. IrrPSNTVerbAVET
	.o. IrrPSNTVerbAXAFT
	.o. IrrPSNTVerbAXI
	.o. IrrPSNTVerbDA
	.o. IrrPSNTVerbKET
	.o. IrrPSNTVerbDAGIRT
];

define IrregularVerbMorph [ 
		[  {axaft} -> {axivî} ||  _ "^WIBA^"    ]
		.o. [  {axaft} -> {axivî} ||  _ "^WIBE^"    ]
		.o. [  {axaft} -> {axivî} ||  _ "^FTRE^"    ]
		.o. [  {axaft} -> {axiv} ||  _ "^IE^"    ]
		.o. [  {ir} -> 0 ||   "^VPBI^" k _ "^IE^" e     ]
		.o. [  {ir} -> 0 ||   "^VPBI^" k _ "^FTRE^" ê     ]
		.o. [  {t} -> v ||   "^VPBI^" k e _ "^IE^" e     ]
		.o. [  {t} -> v ||   "^VPBI^" k e _ "^FTRE^" ê     ]
	 ];

define IrrVerbXIST [
	  [ {xist} -> {xe} || d i "^PSNTDI^" _ "^PSNTDIPOST^"  ]
	.o. [ {xist} -> {x} ||   _ "^FTRE^"  ]
	.o. [ {xist} -> {x} ||  _ "^IE^"  ]
	.o. [ {xist} -> {x} || {'}  _ "^FTRE^"  ]
	.o. [ {xist} -> {x} || {'} _ "^IE^"  ]
	.o. [ {xist} -> {xe} || {'} _ "^PSNTDIPOST^"  ]
];

define IrrVerbGIRT [
	  [ {girt} -> {gire} || d i "^PSNTDI^" _ "^PSNTDIPOST^"  ]
	.o. [ {girt} -> {gir} ||   _ "^FTRE^"  ]
	.o. [ {girt} -> {gir} ||  _ "^IE^"  ]
	.o. [ {girt} -> {gir} || {'}  _ "^FTRE^"  ]
	.o. [ {girt} -> {gir} || {'} _ "^IE^"  ]
	.o. [ {girt} -> {gire} || {'} _ "^PSNTDIPOST^"  ]
];

define IrrVerbBIJART [
	  [ {bijart} -> {bijêrê} || d i "^PSNTDI^" _ "^PSNTDIPOST^"  ]
	.o. [ {bijart} -> {bijêr} ||   _ "^FTRE^"  ]
	.o. [ {bijart} -> {bijêr} ||  _ "^IE^"  ]
	.o. [ {bijart} -> {bijêr} || {'}  _ "^FTRE^"  ]
	.o. [ {bijart} -> {bijêr} || {'} _ "^IE^"  ]
	.o. [ {bijart} -> {bijêrê} || {'} _ "^PSNTDIPOST^"  ]
];

define IrrVerbJEKIR [
	  [ {kir} -> {ke} || d i "^PSNTDI^" _ "^PSNTDIPOST^"  ]
	.o. [ {kir} -> {bik} ||   _ "^FTRE^"  ]
	.o. [ {kir} -> {bike} ||  _ "^IE^"  ]
	.o. [ {kir} -> {k} || {'}  _ "^FTRE^"  ]
	.o. [ {kir} -> {k} || {'} _ "^IE^"  ]
	.o. [ {kir} -> {ke} || {'} _ "^PSNTDIPOST^"  ]
];

define IrrVerbXWEST [
	  [ {xwest} -> {xwaze} ||  "^PSNTDI^" _ "^PSNTDIPOST^"  ]
	.o. [ {xwest} -> {xwaz} ||   _ "^FTRE^"  ]
	.o. [ {xwest} -> {xwaz} ||  _ "^IE^"  ]
];


define IrrVerbXWAR [
	  [ {xwar} -> {xwe} ||  "^PSNTDI^" _ "^PSNTDIPOST^"  ]
	.o. [ {xwar} -> {xw} ||   _ "^FTRE^"  ]
	.o. [ {xwar} -> {xwe} ||  _ "^IE^"  ]
	.o. [ {xwar} -> {xw} || {'}  _ "^FTRE^"  ]
	.o. [ {xwar} -> {xwe} || {'} _ "^IE^"  ]
	.o. [ {xwar} -> {xwe} || {'} _ "^PSNTDIPOST^"  ]
];


define IrrVerbANI [
	[  [ {anî} -> {tênê} ||  "^PSNTDI^" _ "^PSNTDIPOST^"  ]
	.o. [ {anî} -> {bênê} ||   _ "^FTRE^"  ]
	.o. [ {anî} -> {bêne} ||  _ "^IE^"  ]
	.o. [ {anî} -> {bên} || {'}  _ "^FTRE^"  ]
	.o. [ {anî} -> {bêne} || {'} _ "^IE^"  ]
	.o. [ {anî} -> {bênê} || {'} _ "^PSNTDIPOST^"  ]
	]
	.o. [
		[ {bi}->0 || _ "^VPBI^" b ê n [e|ê] ]
	    .o. [ {di}->0 || _ "^PSNTDI^" t ê n [e|ê] ]
	]
];

define IrrVerbGIHIST [
	  [ {şt} -> {je} ||  "^PSNTDI^" g i h i _ "^PSNTDIPOST^"  ]
	.o. [ {şt} -> {j} ||   g i h i  _ "^FTRE^"  ]
	.o. [ {şt} -> {j} ||  g i h i  _ "^IE^"  ]
	.o. [ {şt} -> {j} || {'}  g i h i  _ "^FTRE^"  ]
	.o. [ {şt} -> {j} || {'}  g i h i _ "^IE^"  ]
	.o. [ {şt} -> {j} || {'}  g i h i _ "^PSNTDIPOST^"  ]
];


define IrrVerbSUST [
	 [ 
	      [ {şuşt} -> {şo} ||  "^PSNTDI^" _ "^PSNTDIPOST^"  ]
	  .o. [ {şuşt} -> {şo} ||   _ "^FTRE^"  ]
	  .o. [ {şuşt} -> {şo} ||  _ "^IE^"  ]
	  .o. [ {şuşt} -> {şo} || {'}  _ "^FTRE^"  ]
	  .o. [ {şuşt} -> {şo} || {'} _ "^IE^"  ]
	  .o. [ {şuşt} -> {şo} || {'} _ "^PSNTDIPOST^"  ]
	]
        .o.   [
	  	   [ ê -> 0 ||    "^FTRE^" _ ]
	       .o. [ e -> 0 ||    "^IE^" _ ]

	]
   
];


define IrrVerbSAND [
	  [ {şand} -> {şênê} ||  "^PSNTDI^" _ "^PSNTDIPOST^"  ]
	.o. [ {şand} -> {şênê} ||   _ "^FTRE^"  ]
	.o. [ {şand} -> {şêne} ||  _ "^IE^"  ]
	.o. [ {şand} -> {şênê} || {'}  _ "^FTRE^"  ]
	.o. [ {şand} -> {şêne} || {'} _ "^IE^"  ]
	.o. [ {şand} -> {şênê} || {'} _ "^PSNTDIPOST^"  ]
];


define IrrVerbHAT [
	  [ {di} -> 0 ||  _ "^PSNTDI^" h a t  ]
	.o. [ {bi} -> 0 ||   _ ["^FTRE^" | "^IE^" | "^VPBI^"] h a t  ]
	.o. [ {hat} -> {tê} ||  _ "^PSNTDIPOST^"  ]
	.o. [ {hat} -> {werê} ||   _ "^FTRE^"  ]
	.o. [ {hat} -> {wer} ||  _ "^IE^"  ]
	.o. [ {hat} -> {werê} || {'}  _ "^FTRE^"  ]
	.o. [ {hat} -> {wer} || {'} _ "^IE^"  ]
	.o. [ {hat} -> {werê} || {'} _ "^PSNTDIPOST^"  ]
];

define IrrVerbHIST [
	  [ {hişt} -> {hêle} || d i  "^PSNTDI^" _ "^PSNTDIPOST^"  ]
	.o. [ {hişt} -> {hêlê} || b i "^VPBI^"  _ "^FTRE^"  ]
	.o. [ {hişt} -> {hêle} || b i  "^VPBI^" _ "^IE^"  ]
	.o. [ {hişt} -> {jê} || {'}  _ "^FTRE^"  ]
	.o. [ {hişt} -> {je} || {'} _ "^IE^"  ]
	.o. [ {hişt} -> {je} || {'} _ "^PSNTDIPOST^"  ]
];

define IrrVerbRAHIST [
	 [ [ {rahişt} -> {raje} ||  "^PSNTDI^" _ "^PSNTDIPOST^"  ]
	.o. [ {rahişt} -> {rajê} ||   _ "^FTRE^"  ]
	.o. [ {rahişt} -> {raje} ||  _ "^IE^"  ]
	.o. [ {rahişt} -> {rajê} || {'}  _ "^FTRE^"  ]
	.o. [ {rahişt} -> {raje} || {'} _ "^IE^"  ]
	.o. [ {rahişt} -> {raje} || {'} _ "^PSNTDIPOST^"  ]
	]
	.o. [  
		[ {bi}->0  || _ "^VPBI^" r a j [e|ê] ]
	]
];


define IrrVerbLEYIST [
	  [ {lêyist} -> {lêyize} || d i "^PSNTDI^" _ "^PSNTDIPOST^"  ]
	.o. [ {lêyist} -> {lêyiz} ||   _ "^FTRE^"  ]
	.o. [ {lêyist} -> {lêyiz} ||  _ "^IE^"  ]
];


define IrrVerbKET [

	  [{t} -> {v} ||  "^PSNTDI^" k e _   ]
	.o.  [{t} -> {v} ||  "^PSNTDI^" k e _  "^PSNTDIPOST^"  ]
	.o. [ {t} -> {v} ||   k e _ "^FTRE^"  ]
	.o. [ {t} -> {v} ||  k e _ "^IE^"  ]
	.o. [ {t} -> {v} || {'} k e  _ "^FTRE^"  ]
	.o. [ {t} -> {v} || {'} k e _ "^IE^"  ]
	.o. [ {t} -> {ve} || {'} k e _ "^PSNTDIPOST^"  ]
];

define IrrVerbMA [

	  [{ma} -> {mênê} ||  "^PSNTDI^"  _   ]
	.o.  [{ma} -> {mênê} ||  "^PSNTDI^"  _  "^PSNTDIPOST^"  ]
	.o. [ {ma} -> {mênê} ||    _ "^FTRE^"  ]
	.o. [ {ma} -> {mêne} ||   _ "^IE^"  ]
];

define IrrVerbDIT [
	  [{dît} -> {bîne} ||  "^PSNTDI^"  _   ]
	.o.  [{dît} -> {bîn} ||  "^PSNTDI^"  _  "^PSNTDIPOST^"  ]
	.o. [ {dît} -> {bîn} ||    _ "^FTRE^"  ]
	.o. [ {dît} -> {bîn} ||   _ "^IE^"  ]
];

define IrrVerbFIROT [
	  [{firot} -> {firoşe} ||  "^PSNTDI^"  _   ]
	.o.  [{firot} -> {firoş} ||  "^PSNTDI^"  _  "^PSNTDIPOST^"  ]
	.o. [ {firot} -> {firoş} ||    _ "^FTRE^"  ]
	.o. [ {firot} -> {firoş} ||   _ "^IE^"  ]
]; 

define IrregularVerbs [
	IrregularVerbMorph
	.o. IrrVerbXIST
	.o. IrrVerbGIRT
	.o. IrrVerbKET
	.o. IrrVerbDIT
	.o. IrrVerbFIROT
	.o. IrrVerbBIJART
	.o. IrrVerbJEKIR
	.o. IrrVerbLEYIST
	.o. IrrVerbMA
	.o. IrrVerbANI
	.o. IrrVerbXWEST
	.o. IrrVerbXWAR
	.o. IrrVerbGIHIST
	.o. IrrVerbSUST
	.o. IrrVerbSAND
	.o. IrrVerbHAT
	.o. IrrVerbRAHIST
	.o. IrrVerbHIST
]; 

define ReorderPrefixBI [
	[
	[0->0] 
	| [ [ {'} -> {'bi}   ||  b i "^VPBI^" VerbPrefixes  _  ]
	.o. [ {bi} -> 0   ||  _ "^VPBI^" VerbPrefixes  '   ]
	.o. [ "^VPBI^" -> 0   ||  _ VerbPrefixes  '   ] ]
	.o. [ {bibi}-> {bi}  ]  !adder for bijart verb, bibijart is not accepted in prefixed productions.
	]
];

define ReorderPrefixDI [
	[
	[0->0] 
	| [ 
		    [ {'} -> {'di}   ||  d i "^VPDI^" VerbPrefixes  _   ]
		.o. [ {di} -> 0   ||  _ "^VPDI^" VerbPrefixes  '   ]
		.o. [ "^VPDI^" -> 0   ||  _ VerbPrefixes  '   ] ]
	| [
		    [ {'} -> {'di}   ||  d i "^PSNTDI^" VerbPrefixes  _   ]
		.o. [ {di} -> 0   ||  _ "^PSNTDI^" VerbPrefixes  '   ]
		.o. [ "^PSNTDI^" -> 0   ||  _ VerbPrefixes  '   ] ]
	]
	
];

define ReorderPrefixes [
	ReorderPrefixDI  | ReorderPrefixBI
];

define VerbRules [
	VerbAndFutureRule
	.o. IrregularVerbs
	.o. IrregularVerbPSNT
	.o. ReorderPrefixes
	.o. InfinitiveVowelRules
	.o. PhoneticDeletions
];

define ErrornousGenerations [
	["^CAND^" {and} "^WIBA^" {iba}]
];

!# Cleanup: remove morpheme boundaries
define CleanErrors ErrornousGenerations -> 0;
define Cleanup Symbol  -> 0;

define Grammar 
           VerbLexicon
           .o. VerbRules 
           !.o. Lexicon 
           !.o. VerbSuffixes
	   !.o. CleanErrors	
	   !.o. Cleanup	
           ;

regex Grammar;

save stack biafirand.fst
