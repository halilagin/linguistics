clear stack
read lexc biafirand.lexc
define Lexicon;



define V [a|e|i|u|o|î|û];
define C [b|c|d|e|f|g|h|j|k|l|m|n|p|q|r|s|t|v|w|x|y|z];
define CT1 [t];
define CT2 [n|x];

define Symbol [
	"^[" 		| 
 	"^]" 		| 
 	"^VPBI^"	| 
 	"^VPDI^"	| 
 	"^VPDI^"	| 
 	"^TFTR^"	| 
 	"^TPST^"	| 
 	"^CAND^"	| 
 	"^CENE^"	| 
 	"^IENE^"	| 
 	"^IE^"		| 
 	"^FTRE^"	| 
 	"^PSNTDI^"	| 
 	"^PSNTDIPOST^"	| 
 	"^WIBA^"	| 
 	"^WIBE^"	| 
 	"^INF1^"	| 
 	"^INF2^"	| 
 	":" 		| !PB prefix boundary 
 	"^NEG^" 	| 
 	"^PAST^" 	| 
 	"^PERF^" 	| 
 	"^PASS^" 	| 
 	"^ASTEM^" 	| 
 	"^MONOV^" 	| 
 	"^MONOVOK^" 	| 
 	"^OREF^" 	| 
 	^HXX^ 	
 ] ;

!define BIvftrdeld [.a n d.] -> e n e ||   _ ("@R.BI.ON@")  ^  ;

define VStemRoot [
	 !0 : 0
	{der:xist}: {der:xist}
];

! bi and di are prefixes of  imperative&aspect and aspect, respectively. These are not included in the list below, because their places change according to syntactic rules. The prefixes below are always prefixed in the same slot in the template morphology of Kurdish.
define VerbPrefixes [{der} | {ber} | {da} | {pê} | {lê} | {jê}  | {hil} | {ser} | {na} | {ne} | {ve} | {vê} | {ra} | {çê} | {rû} | {tê}  ];


define VStemRootNoAnd [
	 0 : 0
	 !{dakut}  : {dakut} 
];

define VStem [
	VStemRoot
	| VStemRootNoAnd
];

define VerbPrefixBI [
	   [{bi}"^VPBI^" ]  
];

define VerbPrefixDI [
	   [{di}"^VPDI^" ] 
];

define VerbPrefixPSNTDI [
	   [{di}"^PSNTDI^" ] 
];

define VerbSuffixCAND [
	   ["^CAND^"{and} ] 
];

define VerbSuffixCENE [
	   ["^CENE^"{ênê} ] 
];

define VerbSuffixIENE [
	   ["^IENE^"{êne} ] 
];

define VerbSuffixFTRE [
	    ["^FTRE^"{ê} ]   
];

define VerbSuffixIE [
	   ["^IE^"{e} ] 
];

define VerbSuffixWIBA [
	   ["^WIBA^"{iba} ] 
];

define VerbSuffixWIBE [
	   ["^WIBE^"{ibe} ] 
];

define VerbSuffixINF1 [
	   ["^INF1^"{in} ] 
];

define VerbSuffixINF2 [
	   ["^INF2^"{în} ] 
];


!define VerbWithAnd [
!	 [ [VStemRootNoAnd "+Causative" ]:[VStemRootNoAnd VerbSuffixCAND] ]
!];

!define Verbs [ 
!	VerbWithAnd
!	];


define Lex [
	   [ [VStemRoot "+Past" ]:[ VStemRoot] ]
	 | [ [VStemRoot "+PresentAspect" "+Past" ]:[ VerbPrefixDI VStemRoot] ]
	 | [ [VStemRootNoAnd "+Causative" "+Past" ]:[ VStemRootNoAnd VerbSuffixCAND] ]
	 | [ [VStemRootNoAnd "+Causative" "+PresentAspect" "+Past" ]:[ VerbPrefixDI VStemRootNoAnd VerbSuffixCAND] ]
	 | [ [VStemRootNoAnd "+Causative" "+Future" ]:[ VerbPrefixBI VStemRootNoAnd VerbSuffixCENE] ]
	 | [ [VStemRootNoAnd "+Causative" "+Imperative" ]:[ VerbPrefixBI VStemRootNoAnd VerbSuffixIENE] ]
	 | [ [VStemRoot  "+Imperative" ]:[ VerbPrefixBI VStemRoot VerbSuffixIE] ]
	 | [ [VStemRootNoAnd "+Causative" "+Present" ]:[ VerbPrefixDI  VStemRootNoAnd VerbSuffixCENE] ]
	 | [ [VStemRoot "+Past" "+Wish" ]:[ (VerbPrefixBI) VStemRoot VerbSuffixWIBA] ]
	 | [ [VStemRootNoAnd "+Causative" "+Past" "+Wish" ]:[ (VerbPrefixBI) VStemRootNoAnd VerbSuffixCAND VerbSuffixWIBA] ]

	 | [ [VStemRoot  "+Future" ]:[ VerbPrefixBI VStemRoot VerbSuffixFTRE] ]
	 | [ [VStemRoot "+Present" "+Wish" ]:[ (VerbPrefixDI) VStemRoot VerbSuffixWIBA] ]
	 | [ [VStemRoot  "+Present"  ]:[ VerbPrefixPSNTDI VStemRoot "^PSNTDIPOST^" ] ]
	 !| [ [VStemRootNoAnd "+Causative" "+PresentAspect" "+Past"  "+Wish" ]:[ (VerbPrefixDI) VStemRootNoAnd VerbSuffixCAND VerbSuffixWIBA] ]
	 | [ [VStemRootNoAnd "+Causative" "+Present" "+Wish" ]:[ (VerbPrefixDI) VStemRootNoAnd VerbSuffixCAND VerbSuffixWIBE] ]
	 | [ [VStemRoot  "+Present" "+Wish" ]:[ (VerbPrefixDI) VStemRoot  VerbSuffixWIBE] ]

	 | [ [VStemRootNoAnd "+INF"  ]:[  VStemRootNoAnd VerbSuffixCAND VerbSuffixINF1] ]

	 | [ [VStemRoot "+INF"  ]:[  VStemRoot [ VerbSuffixINF1 | VerbSuffixINF2] ] ]
];

define InfinitiveVowelRules [
	  [ î -> i || CT1  "^INF2^" _ n  ]	
	.o. [ i -> î || CT2  "^INF1^" _ n  ]	
	.o. [ i -> î || CT2  "^INF2^" _ n  ]	
];



define DeleteDoubleIForInfinitive [
       [ î -> 0 || î["^INF1^"|"^INF2^"] _ n]
   .o. [ î -> 0 || i["^INF1^"|"^INF2^"] _ n]
   .o. [ i -> 0 || i["^INF1^"|"^INF2^"] _ n]
   .o. [ i -> 0 || î["^INF1^"|"^INF2^"] _ n]
];

define DeleteDoubleIForWish [
       [ i -> 0 || [î|i] "^WIBA^" _ b a ]
       .o. [ i -> 0 || [î|i] "^WIBE^" _ b e ]
];

define DeleteIBeforeFutureE [
       [ î -> 0 || _ "^FTRE^" ê ]
];

define DeleteIBeforeIE [
       [ î -> 0 || _ "^IE^" e ]
];

define DeleteIOfWIBAAfterA [
       [ i -> 0 || a "^WIBA^" _ b a ]
       .o. [ i -> 0 || a "^WIBE^" _ b e ]
];

define DltABfrEFTREIE [
       [ a -> 0 || _ "^FTRE^" ê ]
       .o. [ a -> 0 || _ "^IE^" e ]
];

define DltUBfrEFTREIE [
       [ û -> 0 || _ "^FTRE^" ê ]
       .o. [ û -> 0 || _ "^IE^" e ]
       .o. [ i -> 0 || û "^WIBA^" _ b a ]
       .o. [ i -> 0 || û "^WIBE^" _ b e ]
       .o. [ {bû} -> 0 || d i "^VPDI^" _ "^WIBA^"  b a ]
       .o. [ {bû} -> 0 || d i "^VPDI^" _ "^WIBE^"  b e ]
];

define DeletePrefixBIE [
       [ i -> 0 ||  b _ "^VPBI^" "^CENE^" ê ]
       .o. [ i -> 0 ||  b _ "^VPBI^" "^CENE^" ê ]
       .o. [ i -> 0 ||  b _ "^VPBI^" "^IENE^" ê ]
];

define DltIBfrEPSNT [
       [ î -> e ||  _ "^PSNTDIPOST^"   ]
       .o. [ û -> e ||  _ "^PSNTDIPOST^"   ]
];

define PhoneticDeletions [
	DltIBfrEPSNT .o. DeleteDoubleIForInfinitive .o. DeleteDoubleIForWish .o. DeleteIBeforeFutureE .o. DeletePrefixBIE .o. DeleteIBeforeIE .o. DeleteIOfWIBAAfterA .o. DltABfrEFTREIE .o. DltUBfrEFTREIE  
];


define MorphRules [ 
		[  [.a n d.] -> e n e ||  "^VPBI^" VStemRootNoAnd "^CAND^"  _ .#. ]
	 ];

define IrrPSNTVerbANI [
	 [  {di} -> {tê} || _ "^PSNTDI^" a n î   ] 
 	.o. [  {anî} -> {nê} ||  t ê "^PSNTDI^" _   ]
];

define IrrPSNTVerbAVET [
	 [  {di} -> {t} || _ "^PSNTDI^" a v ê t   ] 
 	.o. [  {t} -> {je} ||  t  "^PSNTDI^" a v ê _   ]
];

define IrrPSNTVerbAXAFT [
	 [  {aft} -> {ive} ||  "^PSNTDI^" a x _   ] 
];
define IrrPSNTVerbAXI [
	   {î} -> {îne} ||  "^PSNTDI^" a x _    
];

define IrrPSNTVerbDA [
	   [ {a} -> {e} ||  "^PSNTDI^" d _ .#. ]
];

define IrrPSNTVerbKET [
	   {t} -> {ve} ||  "^PSNTDI^" k e _ 
];

define IrrPSNTVerbDAGIRT [
	    [ {dagirt} -> {digire} ||  "^PSNTDI^" _ "^PSNTDIPOST^" ]
	   | [ {dagirt} -> {gire} ||  "^PSNTDI^" _ "^PSNTDIPOST^" ]
];

define IrregularVerbPSNT [
	[  {ajot} -> {ajo} ||   "^PSNTDI^" _    ]
	.o. IrrPSNTVerbANI
	.o. IrrPSNTVerbAVET
	.o. IrrPSNTVerbAXAFT
	.o. IrrPSNTVerbAXI
	.o. IrrPSNTVerbDA
	.o. IrrPSNTVerbKET
	.o. IrrPSNTVerbDAGIRT
];

define IrregularVerbMorph [ 
		[  {axaft} -> {axivî} ||  _ "^WIBA^"    ]
		.o. [  {axaft} -> {axivî} ||  _ "^WIBE^"    ]
		.o. [  {axaft} -> {axivî} ||  _ "^FTRE^"    ]
		.o. [  {axaft} -> {axiv} ||  _ "^IE^"    ]
		.o. [  {ir} -> 0 ||   "^VPBI^" k _ "^IE^" e     ]
		.o. [  {ir} -> 0 ||   "^VPBI^" k _ "^FTRE^" ê     ]
		.o. [  {t} -> v ||   "^VPBI^" k e _ "^IE^" e     ]
		.o. [  {t} -> v ||   "^VPBI^" k e _ "^FTRE^" ê     ]
	 ];

define IrrVerbXIST [
	  [ {xist} -> {xe} || d i "^PSNTDI^" _ "^PSNTDIPOST^"  ]
	.o. [ {xist} -> {x} || b i "^VPBI^" _ "^FTRE^"  ]
	.o. [ {xist} -> {x} || b i "^VPBI^" _ "^IE^"  ]
	.o. [ {xist} -> {x} || {:}  _ "^FTRE^"  ]
	.o. [ {xist} -> {x} || {:} _ "^IE^"  ]
	.o. [ {xist} -> {xe} || {:} _ "^PSNTDIPOST^"  ]
];

define IrregularVerbs [
	IrregularVerbMorph
	.o. IrrVerbXIST
]; 

define ReorderPrefixBI [
	[ {:} -> {:bi^VPBI^}   ||  b i "^VPBI^" VerbPrefixes  _   ]
	.o. [ {bi} -> 0   ||  _ "^VPBI^" VerbPrefixes  {:}   ]
	.o. [ "^VPBI^" -> 0   ||  _ VerbPrefixes  {:}   ]
];

define ReorderPrefixDI [
	[ {:} -> {:di^VPDI^}   ||  d i "^VPDI^" VerbPrefixes  _   ]
	.o. [ {di} -> 0   ||  _ "^VPDI^" VerbPrefixes  {:}   ]
	.o. [ "^VPDI^" -> 0   ||  _ VerbPrefixes  {:}   ]
];

define ReorderPrefixes [
	ReorderPrefixBI .o. ReorderPrefixDI
];

define Rules [
	MorphRules
	.o. IrregularVerbs
	.o. IrregularVerbPSNT
	.o. ReorderPrefixes
	.o. InfinitiveVowelRules
	.o. PhoneticDeletions
];

define ErrornousGenerations [
	["^CAND^" {and} "^WIBA^" {iba}]
];

!# Cleanup: remove morpheme boundaries
define CleanErrors ErrornousGenerations -> 0;
define Cleanup Symbol  -> 0;

define Grammar 
           Lex
           .o. Rules 
           !.o. Lexicon 
           !.o. VerbSuffixes
	   !.o. CleanErrors	
	   !.o. Cleanup	
           ;

regex Grammar;

save stack biafirand.fst
